import org.gradle.internal.jvm.Jvm

plugins {
    id 'java'
    id 'cpp'
}

group 'com.stmtok'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    systemProperty "java.library.path", file("${buildDir}/libs/stmtok/shared").absolutePath
    useJUnitPlatform()
}

task generateJniHeaders(type: JavaCompile) {
    classpath = sourceSets.main.compileClasspath
    destinationDir file("${buildDir}/generated/jni")
    source = sourceSets.main.java
    options.compilerArgs += [
            '-h', file("${buildDir}/generated/jni"),
            '-d', file("${buildDir}/generated/jni-tmp")
    ]
    doLast {
        delete file("${buildDir}/generated/jni-tmp")
    }
}

sourceCompatibility = JavaVersion.VERSION_11
model {
    platforms {
        x64 {
            architecture "x64"
        }
    }

    components {
        stmtok(NativeLibrarySpec) {
            targetPlatform "x64"
            binaries.all {
                if (targetPlatform.operatingSystem.macOsX) {
                    cppCompiler.args '-I', "${Jvm.current().javaHome}/include"
                    cppCompiler.args '-I', "${Jvm.current().javaHome}/include/darwin"
                    cppCompiler.args '-I', "${buildDir}/generated/jni"
                } else if (targetPlatform.operatingSystem.linux) {
                    cppCompiler.args '-I', "${Jvm.current().javaHome}/include"
                    cppCompiler.args '-I', "${Jvm.current().javaHome}/include/linux"
                    cppCompiler.args '-I', "${buildDir}/generated/jni"
                    cppCompiler.args '-D_FILE_OFFSET_BITS=64'
                } else if (targetPlatform.operatingSystem.windows) {
                    cppCompiler.args "-I${Jvm.current().javaHome}/include"
                    cppCompiler.args "-I${Jvm.current().javaHome}/include/win32"
                    cppCompiler.args '-I', "${buildDir}/generated/jni"
                    linker.args "Shlwapi.lib", "Advapi32.lib"
                }
            }
        }
    }
}

test.dependsOn 'stmtokSharedLibrary'
assemble.dependsOn generateJniHeaders
